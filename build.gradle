buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.postgresql:postgresql:42.7.3'
        classpath 'org.flywaydb:flyway-database-postgresql:11.10.3'
    }
}

plugins {
    id 'java'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.springframework.boot' version '3.3.0'
    id 'org.owasp.dependencycheck' version '10.0.3'
    id 'org.flywaydb.flyway' version '11.10.3'
}

group = 'org.example'
version = '1.0-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

repositories {
    mavenCentral()
}

flyway {
    driver = 'org.postgresql.Driver'
    url = project.findProperty('flywayUrl') ?: 'jdbc:postgresql://localhost:5432/inventario_db'
    user = project.findProperty('flywayUser') ?: 'postgres'
    password = project.findProperty('flywayPassword') ?: 'admin'
    locations = ['classpath:db/migration']
//    baselineOnMigrate = true
//    createSchemas = true
//    connectRetries = 3
}

dependencies {
    // Spring Boot Starters
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Utilities
    implementation 'org.modelmapper:modelmapper:3.2.3'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.2.0'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Database
    implementation 'org.flywaydb:flyway-core:11.10.3'
    implementation 'org.flywaydb:flyway-database-postgresql:11.10.3'
    implementation 'org.postgresql:postgresql:42.7.3'

    // Testing
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'junit', module: 'junit'
    }
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testImplementation 'org.mockito:mockito-core:5.10.0'
    testImplementation 'io.cucumber:cucumber-java:7.23.0'
    testImplementation 'io.cucumber:cucumber-spring:7.23.0'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine:7.23.0'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'io.cucumber:cucumber-junit:7.23.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = "full"
    }
    if (project.hasProperty("profile")) {
        systemProperty "spring.profiles.active", project.getProperty("profile")
    }
}